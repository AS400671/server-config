# useful functions for export and import

/*
    # is my network
    is_self_net_v4()
    is_self_net_v6()

    # check ASN
    is_my_asn()
    is_peer_asn()

    # Partially referenced from
    # https://bgpfilterguide.nlnog.net/
    respect_graceful_shutdown()
    is_martian_v4()
    is_martian_v6()
    is_small_prefix_v4()
    is_small_prefix_v6()
    is_martian_asn()
    is_valid_rpki()

    # For customers / IXP only
    in_transit_path()
    is_short_aspath()
*/

function is_self_net_v4() -> bool
{
    if(net ~ my_route_ipv4) then
    {
        bgp_large_community.add((my_asn, my_router_id, 100));
        bgp_local_pref = 300;
        return true;
    }
    return false;
}

function is_self_net_v6() -> bool
{
    if(net ~ my_route_ipv6) then
    {
        bgp_large_community.add((my_asn, my_router_id, 100));
        bgp_local_pref = 300;
        return true;
    }
    return false;
}

function is_my_asn() -> bool
{
    return bgp_path.last ~ [ my_asn ];
}

function is_peer_asn() -> bool
{
    return bgp_path.last ~ [
        203979,      # Misaka Mikoto Network
        210576       # YAGUMYAGUM
    ];
}

/*******/

function respect_graceful_shutdown() -> bool
{
    if (65535, 0) ~ bgp_community then
    {
        bgp_local_pref = 0;
    }
}

function is_martian_v4() -> bool
{
    return net ~ [
        0.0.0.0/8+,
        10.0.0.0/8+,
        100.64.0.0/10+,
        127.0.0.0/8+,
        169.254.0.0/16+,
        172.16.0.0/12+,
        192.0.2.0/24+,
        192.88.99.0/24+,
        192.168.0.0/16+,
        193.254.225.0/24+,
        198.18.0.0/15+,
        198.51.100.0/24+,
        203.0.113.0/24+,
        224.0.0.0/4+,
        240.0.0.0/4+
    ];
}

function is_martian_v6() -> bool
{
    return net ~ [
        ::/8+,
        ::ffff:0.0.0.0/96+,
        ::/96,
        ::/128,
        ::1/128,
        0000::/8+,
        0100::/64+,
        0200::/7+,
        2001:2::/48+,
        2001:10::/28+,
        2001:db8::/32+,
        2002:e000::/20+,
        2002:7f00::/24+,
        2002:0000::/24+,
        2002:ff00::/24+,
        2002:0a00::/24+,
        2002:ac10::/28+,
        2002:c0a8::/32+,
        3ffe::/16+,
        fc00::/7+,
        fe80::/10+,
        fec0::/10+,
        ff00::/8+
    ];
}

function is_martian_asn() -> bool
{
    # 64512 .. 65534 is explicitly disabled for internal routing
    return bgp_path ~ [
        0,
        23456,
        64496..64511,
        65535..131701,
        4200000000..4294967294,
        4294967295
    ];
}

function is_small_prefix_v4() -> bool
{
    return net.len > 24;
}

function is_small_prefix_v6() -> bool
{
    return net.len > 48;
}

function is_long_aspath() -> bool
{
    return bgp_path.len > 100;
}

function is_invalid_rpki() -> bool
{
    return (roa_check(r4, net, bgp_path.last) = ROA_INVALID ||
            roa_check(r6, net, bgp_path.last) = ROA_INVALID);
}

function is_dprk_route() -> bool
{
    return (bgp_path ~ [ 121279 ] ||
        net ~ [
            5.62.56.160/30+,
            5.62.61.64/30+,
            45.42.151.0/24+,
            46.36.203.80/30+,
            57.73.224.0/19+,
            82.146.88.0/24+,
            88.151.117.0/24+,
            95.141.91.192/29+,
            172.97.82.128/25+,
            175.45.176.0/22+,
            185.56.163.144/28+,
            196.196.114.0/24+,
            196.197.114.0/24+,
            196.198.114.0/24+,
            196.199.114.0/24+,
            202.72.96.4/30+,
            210.52.109.0/24+,
            # /32
            185.51.35.159/32+,
            185.196.36.83/32+,
            188.114.227.151/32+,
            45.12.70.122/32+,
            45.12.71.122/32+,
            46.36.203.81/32+,
            82.102.0.129/32+,
            94.46.49.204/32+,
            194.50.99.122/32+,
            194.50.111.122/32+,
            193.58.239.122/32+
        ]);
}

/*******/

function is_short_aspath() -> bool
{
    return bgp_path.len < 1;
}

function in_transit_paths() -> bool
{
    # Block Tier 1 Transit ASNs
    return bgp_path ~ [
        174,
        701,
        1299,
        2914,
        3257,
        3320,
        3356,
        3491,
        4134,
        5511,
        6453,
        6461,
        6762,
        6830,
        7018
    ];
}
